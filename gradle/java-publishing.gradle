assert plugins.hasPlugin("java")

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

tasks.withType(Jar) {
    from "$rootDir/LICENSE"
    from "$rootDir/NOTICE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/linkedin/ambry/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/linkedin/ambry.git'
                }
                issueManagement {
                    url = 'https://github.com/linkedin/ambry/issues'
                    system = 'GitHub issues'
                }
                ciManagement {
                    url='https://travis-ci.org/linkedin/ambry'
                    system='TravisCI'
                }
            }
        }
    }
}

bintray { //docs: https://github.com/bintray/gradle-bintray-plugin
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    publish = true
    dryRun = project.hasProperty("bintray.dryRun") //useful for testing

    publications = ['maven']

    pkg {
        repo = 'maven'
        userOrg = 'linkedin'
        name = 'ambry'

        licenses = ['Apache-2.0']
        labels = ['blob storage']
        version {
            // disable gpg signing to speed up publishing
            gpg {
                sign = false
            }
            // disable upload to maven central
            mavenCentralSync {
                sync = false
            }
        }
    }
}

bintrayUpload {
    doFirst {
        println "Publishing $jar.baseName to Bintray (dryRun: $dryRun, repo: $repoName, publish: $publish)"
    }
}
